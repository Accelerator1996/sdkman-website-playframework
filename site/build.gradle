import com.yahoo.platform.yui.compressor.CssCompressor
import com.yahoo.platform.yui.compressor.JavaScriptCompressor
import org.apache.tools.ant.filters.BaseFilterReader

ext.watchmode = project.hasProperty('watchmode')?project.getProperty('watchmode'):'false'

apply plugin: 'base'

task copyAssets(type:Copy) {
    from file('src/site/assets')
    into file("$buildDir/site")
    filesMatching('**/*.css') { f->
        if (!f.name.contains('.min.')) {
            filter(CssFilter)
        }
    }
    filesMatching('**/*.js') { f->
        if (!f.name.contains('.min.')) {
            filter(JsFilter)
        }
    }
}

task copyCname(type:Copy) {
    from file('src/site/CNAME')
    into file("$buildDir/site")
}

task fetchCandidates(type:Copy) {
    GFileUtils.copyURLToFile(new URL('https://api.sdkman.io/2/candidates/list'), new File('build/candidates.txt'));
}

task convertCandidates(type:Copy) {
    dependsOn fetchCandidates

    String s = GFileUtils.readFile(new File('build/candidates.txt'))
    String gen = '';
    // split the text for each candidate
    def elements = s.split('----*\\n')

    elements.eachWithIndex { String entry, int i ->
        // ignore the 1st element. Candidates start at index 1
        if(i > 0) {
            def parts = entry.split('\\n\\n');

            String title = parts[0];
            String[] titleParts = title.split("(?=http\\:\\/\\/)|(?=https\\:\\/\\/)")

            def candidate = titleParts[0].substring(0, titleParts[0].indexOf("("))

            gen+="<h4>"+candidate?.trim()+"</h4>\n"
            gen+="<a href='"+titleParts[1]?.trim()+"' target='_blank'>"+titleParts[1]+"</a></br>\n"
            gen+="<p>"+parts[1]+"</p>\n" // content
            gen+="<code>"+parts[2].trim()+"</code>\n" // footer
            gen+="<hr/>\n"
        }
    }
    GFileUtils.writeStringToFile(new File('./site/src/site/html/candidates.html'),gen)
}

task generateSite(type:JavaExec) {

    description = 'Generates the Groovy Website'
    dependsOn copyAssets, copyCname, convertCandidates

    ext.sources = file('src/site')
    ext.outputDir = file("$buildDir/site")

    inputs.files fileTree(sources)
    outputs.files fileTree(outputDir)

    classpath = project(':generator').sourceSets.main.runtimeClasspath
    main = 'generator.SiteGenerator'
    args = [sources, outputDir, project.watchmode]
    systemProperties.docs_baseurl = System.getProperty('docs_baseurl')
}

task webzip(type:Zip, dependsOn: generateSite) {
    description = "Creates a zip with the generated website and the deadlink report"
    destinationDir = file("$buildDir/distributions")

    baseName = 'sdkman'
    appendix = 'website'
    from(generateSite.outputDir) {
        into '.'
    }
}

// Resource filtering classes

class JsFilter extends BaseFilterReader {
    Writer writer
    Thread worker

    JsFilter(Reader reader) {
        super(new PipedReader())
        writer = new PipedWriter(this.@in)
        def compressor = new JavaScriptCompressor(reader, null)
        reader.close()
        worker = Thread.start {
            compressor.compress(writer, -1, true, false, false, false)
            writer.close()
        }
    }

    void close() {
        worker.join()
        super.close()
    }

}

class CssFilter extends BaseFilterReader {
    Writer writer
    Thread worker

    CssFilter(Reader reader) {
        super(new PipedReader())
        writer = new PipedWriter(this.@in)
        def compressor = new CssCompressor(reader)
        reader.close()
        worker = Thread.start {
            compressor.compress(writer, -1)
            writer.close()
        }
    }

    void close() {
        worker.join()
        super.close()
    }

}

