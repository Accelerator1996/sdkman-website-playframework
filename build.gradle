import groovy.text.markup.MarkupTemplateEngine
import groovy.text.markup.TemplateConfiguration
import org.gradle.logging.ConsoleRenderer

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.codehaus.groovy:groovy:2.3.3'
        classpath 'org.codehaus.groovy:groovy-templates:2.3.3'
    }
}

apply plugin:'groovy'

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy:2.3.3'
    compile 'org.codehaus.groovy:groovy-templates:2.3.3'
}

task copyAssets(type:Copy) {
    from file('src/site/assets')
    into file("$buildDir/site")
}

task generateSite(dependsOn: copyAssets) {

    ext.sources = file('src/site')
    ext.outputDir = file("$buildDir/site")

    inputs.files fileTree(sources)
    outputs.files fileTree(outputDir)

    doLast {
        def tplConf = new TemplateConfiguration()
        tplConf.with {
            autoIndent = true
            autoNewLine = true
        }
        def tplEngine = new MarkupTemplateEngine(this.class.classLoader, sources, tplConf)

        def siteMap = SiteMap.from(file('sitemap.groovy'))

        def render = { String page, String target = null, Map model = [:] ->
            model.menu = siteMap.menu
            target = target ?: page
            file("$outputDir/${target}.html").write(tplEngine.createTemplateByPath("pages/${page}.groovy").make(model).toString(), 'utf-8')
        }


        render 'index'
        render 'ecosystem', 'ecosystem', [ecosys: siteMap.ecosystem]
        render 'learn', 'learn', [docSections: siteMap.documentationSections]

        siteMap.documentationSections.each { Section section ->
            section.items.each { SectionItem item ->
                logger.info("Generating documentation page [$item.name]")
                render 'docpage', item.targetFilename, [title: item.name, iframeTarget: "http://docs.groovy-lang.org/docs/next/html/documentation/${item.sourceFilename}.html"]
            }
        }

        render 'download', 'download', [distributions: siteMap.distributions]
        render 'versioning', 'versioning'
        render 'indy', 'indy'
        render 'community', 'community'
        render 'groovyweekly', 'groovy-weekly'
        render 'api', 'api', [iframeTarget: 'http://docs.groovy-lang.org/docs/next/html/gapi']
        render 'gdk', 'gdk', [iframeTarget: 'http://docs.groovy-lang.org/docs/next/html/groovy-jdk']
    }
}

task checkDeadLinks(dependsOn: generateSite) {
    ext.outputDir = file("$buildDir/reports")
    ext.reportFile = file("$outputDir/deadlinks.html")

    inputs.files fileTree(generateSite.outputDir)
    outputs.file reportFile

    doLast {
        def deadLinks = [:]

        def isDead = [:].withDefault { String link ->
            try {
                URL url
                try {
                    url = URI.create(link).toURL()
                } catch (e) {
                    if (e.message.contains('URI is not absolute')) {
                        url = URI.create("file://${file("${generateSite.outputDir}/$link")}").toURL()
                    }
                }
                def stream = url.openStream()
                stream.close()
            } catch (e) {
                return true
            }
            return false
        }

        def checkLink = { List dead, int line, String link ->
            if (isDead[link]) {
                dead << "at line $line: $link"
            }
        }

        def checkPage = { File f ->
            f.eachLine('utf-8') { String line, int nb ->
                def dead = []
                [/href=['"](.+?)['"]/, /src=['"](.+?)['"]/].each { regex ->
                    def matcher = line =~ regex
                    if (matcher) {
                        matcher.each {
                            checkLink(dead, nb, it[1])
                        }
                    }
                }
                if (dead) {
                    deadLinks[f] = dead
                }
            }
        }

        file(generateSite.outputDir).eachFileRecurse {
            if (it.name.endsWith('.html')) {
                checkPage(it)
            }
        }

        outputDir.mkdirs()
        def tplConf = new TemplateConfiguration()
        tplConf.with {
            autoIndent = true
            autoNewLine = true
        }
        def tplEngine = new MarkupTemplateEngine(this.class.classLoader, file('gradle/templates'), tplConf)

        def report = tplEngine.createTemplateByPath("deadlinks.groovy").make(deadLinks: deadLinks).toString()

        reportFile.write(report, 'utf-8')
        def reportURL = new ConsoleRenderer().asClickableFileUrl(reportFile)
        logger.lifecycle "Dead links report written at $reportURL"
    }
}